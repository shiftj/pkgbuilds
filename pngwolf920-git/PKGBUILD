# Maintainer: shiftj github.com/shiftj

pkgname=pngwolf920-git
pkgver=r14.ff9764b
pkgrel=1
pkgdesc="pngwolf uses a genetic algorithm to find PNG scanline filter combinations that compress well (compiled using 7zip 9.20)"
arch=('i686' 'x86_64')
url="http://bjoern.hoehrmann.de/pngwolf/"
license=('custom')
depends=('')
makedepends=('git' 'sed')
provides=('pngwolf')
conflicts=('pngwolf')
replaces=('pngwolf')
source=('http://www.7-zip.org/a/7z920.tar.bz2' 'http://lancet.mit.edu/ga/dist/galib247.tgz' 'http://zlib.net/current/zlib-1.2.8.tar.gz' 'git://github.com/hoehrmann/pngwolf')
md5sums=('c63583eff4bceb4315228ff4881a7461'
         '2b6a28fd06d4c7c4d0bb39c92b2b376c'
         '44d667c142d7cda120332623eab69f40'
         'SKIP')

pkgver() {
  cd ${pkgname%920-git}
  printf  "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%920-git}

  mkdir -p 7zip
  ln -s ../../CPP  7zip/CPP
  ln -s ../../C 7zip/C
  ln -s ../galib247 galib
  ln -s ../zlib-1.2.8 zlib
  
  # fix 'different line endings' error / 7zip src has windows (CRLF) line endings
  # this adds sed makedepend
  # \x0d is CRLF end of line
  sed -i 's/\x0d$//' 7zip/CPP/7zip/Common/StreamObjects.cpp
  sed -i 's/\x0d$//' 7zip/C/Alloc.h
  sed -i 's/\x0d$//' 7zip/C/Alloc.c
 
  patch -p 1 -d .. < sevenzip920.patch

  # fix galib247 compilation error
  sed -i \
    -e 's/initializer(/this->initializer(/g' \
    -e 's/mutator(/this->mutator(/g' \
    -e 's/comparator(/this->comparator(/g' \
    -e 's/crossover(/this->crossover(/g' \
    ../galib247/ga/GA?DArrayGenome.C

  msg2 "Running cmake..."
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
}

build() {
	cd ${pkgname%920-git}/build

	msg "Starting build process."
  make CXXFLAGS="${CXXFLAGS}" || return 1
}

package() {
  cd ${pkgname%920-git}/build

	msg2 'Installing...'

	install -Dm755 ${pkgname%920-git} ${pkgdir}/usr/bin/${pkgname%-git}

	install -Dm644 ../README ${pkgdir}/usr/share/doc/${pkgname%-git}/README
}

# vim:set ts=2 sw=2 et:
