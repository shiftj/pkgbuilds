# Maintainer: shiftj github.com/shiftj

pkgname=pngwolf-git
pkgver=r14.ff9764b
pkgrel=1
pkgdesc="pngwolf uses a genetic algorithm to find PNG scanline filter combinations that compress well (compiled using 7zip 9.20)"
arch=('i686' 'x86_64')
url="http://bjoern.hoehrmann.de/pngwolf/"
license=('custom')
depends=('zlib')
makedepends=('git' 'zlib')
provides=('pngwolf')
conflicts=('pngwolf')
replaces=('pngwolf')
source=('http://www.7-zip.org/a/7z920.tar.bz2' 'http://lancet.mit.edu/ga/dist/galib247.tgz' 'http://zlib.net/current/zlib-1.2.8.tar.gz' 'git://github.com/hoehrmann/pngwolf')
#source=('http://www.7-zip.org/a/7z938-src.7z' 'http://lancet.mit.edu/ga/dist/galib247.tgz' 'http://zlib.net/current/zlib-1.2.8.tar.gz' 'git://github.com/hoehrmann/pngwolf')
md5sums=('c63583eff4bceb4315228ff4881a7461'
         '2b6a28fd06d4c7c4d0bb39c92b2b376c'
         '44d667c142d7cda120332623eab69f40'
         'SKIP')

pkgver() {
  cd ${pkgname%-git}
  printf  "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p pngwolf/7zip
  cp -r CPP  pngwolf/7zip/CPP
  cp -r C pngwolf/7zip/C
  cp -r Asm pngwolf/7zip/Asm
  ln -s ../galib247 pngwolf/galib
  ln -s ../zlib-1.2.8 pngwolf/zlib

  # fix pngwolf compilation error
  sed -i \
    -e 's/initializer(/this->initializer(/g' \
    -e 's/mutator(/this->mutator(/g' \
    -e 's/comparator(/this->comparator(/g' \
    -e 's/crossover(/this->crossover(/g' \
    galib247/ga/GA?DArrayGenome.C


  cd ${pkgname%-git}

  # fix 'different line endings' error / 7zip src has windows (CRLF) line endings
  tr -d "\r" < 7zip/C/Alloc.h > tmp
  mv tmp 7zip/C/Alloc.h
  tr -d "\r" < 7zip/C/Alloc.c > tmp
  mv tmp 7zip/C/Alloc.c
  tr -d "\r" < 7zip/CPP/7zip/Common/StreamObjects.cpp > tmp
  mv tmp 7zip/CPP/7zip/Common/StreamObjects.cpp

  patch -p 0 < sevenzip920.patch

  cmake .

}

build() {
	cd ${pkgname%-git}

	#aclocal
	#autoheader
	#autoconf
	#automake --add-missing
	#./configure --prefix=/usr

	msg "Starting build process."
  make CXXFLAGS="${CXXFLAGS}" || return 1
}

package() {
  cd ${pkgname%-git}

	msg2 'Installing...'

  install -Dm755 ${pkgname%-git} ${pkgdir}/usr/bin/${pkgname%-git}

	install -Dm644 README ${pkgdir}/usr/share/doc/${pkgname%-git}/README
}

# vim:set ts=2 sw=2 et:
